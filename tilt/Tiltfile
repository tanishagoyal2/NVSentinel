# Copyright (c) 2025, NVIDIA CORPORATION.  All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load('ext://helm_resource', 'helm_resource', 'helm_repo')
load('ext://namespace', 'namespace_create', 'namespace_inject')

update_settings(k8s_upsert_timeout_secs=600)

num_gpu_nodes = int(os.getenv('NUM_GPU_NODES', '50'))
num_kata_test_nodes = int(os.getenv('NUM_KATA_TEST_NODES', '5'))

helm_repo('jetstack', 'https://charts.jetstack.io')
helm_resource(
    'cert-manager',
    chart='jetstack/cert-manager',
    namespace='cert-manager',
    flags=[
        '--create-namespace',
        '--set=installCRDs=true',
    ],
)

helm_repo('prometheus-community', 'https://prometheus-community.github.io/helm-charts')
helm_resource(
    'prometheus-operator',
    chart='prometheus-community/kube-prometheus-stack',
    namespace='monitoring',
    flags=[
        '--create-namespace',
        '--set=prometheus.enabled=true',
        '--set=alertmanager.enabled=false',
        '--set=grafana.enabled=false',
        '--set=kubeStateMetrics.enabled=false',
        '--set=nodeExporter.enabled=false',
        '--set=prometheusOperator.enabled=true',
    ],
)

helm_repo('sigs-kwok', 'https://kwok.sigs.k8s.io/charts/')
helm_resource(
    'kwok',
    chart='sigs-kwok/kwok',
    namespace='kube-system',
    flags=[
        '--set=hostNetwork=true'
    ]
)
helm_resource(
    'kwok-stage-fast',
    chart='sigs-kwok/stage-fast',
    resource_deps=['kwok'],
    pod_readiness='ignore'
)

namespace_create('gpu-operator')
namespace_create('nvsentinel')

skip_kwok_nodes = os.getenv('SKIP_KWOK_NODES_IN_TILT', '0') == '1'

if not skip_kwok_nodes:
    # Create regular GPU nodes (all NUM_GPU_NODES are regular nodes)
    kwok_node_template = str(read_file('./kwok-node-template.yaml'))
    for i in range(num_gpu_nodes):
        node_yaml = kwok_node_template.replace('PLACEHOLDER', str(i))
        k8s_yaml(blob(node_yaml))

    # Create separate Kata test nodes (in addition to regular nodes)
    # These are named differently (kwok-kata-test-node-*) to avoid conflicts
    # Set NUM_KATA_TEST_NODES=0 to disable kata testing
    kwok_kata_test_node_template = str(read_file('./kwok-kata-test-node-template.yaml'))
    for i in range(num_kata_test_nodes):
        node_yaml = kwok_kata_test_node_template.replace('PLACEHOLDER', str(i))
        k8s_yaml(blob(node_yaml))
else:
    print("Skipping KWOK node creation in Tilt (SKIP_KWOK_NODES_IN_TILT=1)")

k8s_yaml('./nvidia-driver-daemonset.yaml')
k8s_yaml('./nvidia-dcgm-daemonset.yaml')

include('../fault-quarantine/Tiltfile')
include('../fault-remediation/Tiltfile')
include('../janitor/Tiltfile')
include('../node-drainer/Tiltfile')
include('../platform-connectors/Tiltfile')
include('./simple-health-client/Tiltfile')
include('../health-events-analyzer/Tiltfile')
include('../health-monitors/gpu-health-monitor/Tiltfile')
include('../health-monitors/syslog-health-monitor/Tiltfile')
include('../labeler/Tiltfile')

values_files = [
    '../distros/kubernetes/nvsentinel/values.yaml',
    '../distros/kubernetes/nvsentinel/values-tilt.yaml',
    '../distros/kubernetes/nvsentinel/values-tilt-socket.yaml'  # Ensures /var/run exists for socket
]

arch = str(local('uname -m')).strip()
if arch in ['arm64', 'aarch64']:
    values_files.append('../distros/kubernetes/nvsentinel/values-tilt-arm64.yaml')

yaml = helm(
    '../distros/kubernetes/nvsentinel',
    name='nvsentinel',
    namespace='nvsentinel',
    values=values_files,
)
k8s_yaml(yaml)

k8s_resource(
    new_name='cert-manager-resources',
    objects=[
        'mongo-root-ca:certificate',
        'mongo-ca-issuer:issuer', 
        'selfsigned-ca-issuer:issuer',
        'mongo-server-cert-0:certificate',
        'mongo-app-client-cert:certificate',
        'mongo-dgxcops-client-cert:certificate',
        'janitor-webhook-cert:certificate'
    ],
    resource_deps=['cert-manager'],
)

k8s_resource(
    new_name='prometheus-resources',
    objects=['nvsentinel:podmonitor'],
    resource_deps=['prometheus-operator'],
)
k8s_resource(
    'prometheus-operator',
    port_forwards='9090:9090',
)

# Group all KWOK nodes (regular GPU nodes + separate kata test nodes) for resource management
if not skip_kwok_nodes:
    kwok_regular_node_names = ['kwok-node-' + str(i) + ':node' for i in range(num_gpu_nodes)]
    kwok_kata_test_node_names = ['kwok-kata-test-node-' + str(i) + ':node' for i in range(num_kata_test_nodes)]
    kwok_all_node_names = kwok_regular_node_names + kwok_kata_test_node_names

    k8s_resource(
        new_name='kwok-fake-nodes',
        objects=kwok_all_node_names,
        resource_deps=['kwok', 'platform-connectors', 'fault-quarantine', 'fault-remediation', 
            'labeler', 'node-drainer', 'mongodb', 'simple-health-client'
        ], 
    )

k8s_resource(
    'gpu-health-monitor-dcgm-4.x',
    resource_deps=['platform-connectors']
)

k8s_resource(
    'syslog-health-monitor-regular',
    resource_deps=['platform-connectors']
)

k8s_resource(
    'simple-health-client',
    resource_deps=['platform-connectors']
)

k8s_resource(
    'platform-connectors',
    resource_deps=['mongodb']
)

k8s_resource(
    'fault-quarantine',
    resource_deps=['mongodb']
)

k8s_resource(
    'node-drainer',
    resource_deps=['mongodb']
)

k8s_resource(
    'fault-remediation',
    resource_deps=['mongodb']
)

k8s_resource(
    'kwok-stage-fast',
    pod_readiness='ignore',
    resource_deps=['kwok']
)
k8s_resource(
    workload='gpu-health-monitor-dcgm-3.x',
    pod_readiness='ignore'
)
k8s_resource(
    workload='syslog-health-monitor-kata',
    pod_readiness='ignore'
)

# This is a workaround to ensure that pods are deleted with respect to finalizers
# source: https://github.com/kubernetes-sigs/kwok/discussions/926#discussioncomment-8204558
k8s_yaml('./kwok-pod-delete-respect-finalizers.yaml')
k8s_resource(
    new_name='kwok-pod-delete-stage',
    objects=['pod-delete:stage'],
    resource_deps=['kwok', 'kwok-stage-fast']
)
